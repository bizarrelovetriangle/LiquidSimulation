#version 430 core
#include "../../common.glsl"

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

void main() {
	int org = particle_indexes[gl_GlobalInvocationID.x];
	if (particles[org].state != 1) return;
	particles[org].state = 0;

	Range range = GetRange(grid_size, particles[org].grid_position);

	for (int y = range.a.y; y <= range.b.y; ++y) {
		for (int x = range.a.x; x <= range.b.x; ++x) {
			GridCell nghrCell = grid[y * grid_size.x + x];

			for (int i = nghrCell.start; i < nghrCell.end; ++i) {
				int index = particle_indexes[i];
				if (particles[org].index == particles[index].index) continue;

				float dist = distance(particles[org].position, particles[index].position);
				if (dist > config.interactionRange || dist == 0) continue;

				int thread_id = atomicAdd(threads_count, 2);
				threads[thread_id].first = org;
				threads[thread_id].second = index;
				threads[thread_id].rest_length = dist;
				threads[thread_id].torn = false;
				
				threads[thread_id + 1].first = index;
				threads[thread_id + 1].second = org;
				threads[thread_id + 1].rest_length = dist;
				threads[thread_id + 1].torn = false;
			}
		}
	}
}
