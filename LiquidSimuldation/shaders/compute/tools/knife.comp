#version 430 core
#include "../../common.glsl"
#define _precision 0.000001

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

layout(location = 0) uniform vec2 scratch_a;
layout(location = 1) uniform vec2 scratch_b;

float cross(vec2 a, vec2 b);

void main() {
	int threads_chunk_size = int(ceil(float(threads_count) / parallel));

	int start = int(gl_GlobalInvocationID.x) * threads_chunk_size;
	int end = min(start + threads_chunk_size, threads_count);

	if (start >= threads_count) return;
	
	for (int i = start; i < end; ++i) {
		if (threads[i].torn) continue;
		vec2 thread_a = particles[threads[i].first].position;
		vec2 thread_b = particles[threads[i].second].position;

		vec2 scratch_a_b = scratch_a - scratch_b;
		vec2 thread_a_b = thread_a - thread_b;

		bool bilateral_scratch =
			cross(thread_a - scratch_b, scratch_a_b) > 0 !=
			cross(thread_b - scratch_b, scratch_a_b) > 0;
		bool bilateral_pair =
			cross(scratch_a - thread_b, thread_a_b) > 0 !=
			cross(scratch_b - thread_b, thread_a_b) > 0;

		if (bilateral_scratch && bilateral_pair) {
			atomicAdd(threads_torn, 1);
			threads[i].torn = true;
		}
	}
}

float cross(vec2 a, vec2 b) {
	return a.x * b.y - a.y * b.x;
}
