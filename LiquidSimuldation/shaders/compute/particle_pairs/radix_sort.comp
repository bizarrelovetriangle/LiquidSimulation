#version 430 core
#include "../../common.glsl"

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

layout(std430, binding = 0) buffer ParticlesInput
{
	Particle particles[];
};
layout(std430, binding = 1) buffer PairsCount
{
	int pairs_count;
};
layout(std430, binding = 2) buffer PairsOutput
{
	PairData pairs[];
};
layout(std430, binding = 3) buffer PairsTempOutput
{
	PairData pairs_temp[];
};
layout(std430, binding = 4) buffer DistributedBucketIndexesCountOutput
{
	int distributed_bucket_indexes[];
};
layout(std430, binding = 5) buffer DistributedBucketsCountOutput
{
	int distributed_buckets[];
};

layout(location = 0) uniform ivec2 gridSize;
layout(location = 1) uniform int dimension;
layout(location = 2) uniform int byte;
layout(location = 3) uniform int chunk_size;

void main() {
	const int buckets_size = 1 << 8;
	const int mask = buckets_size - 1;
	const int shift = byte * 8;
	const uint buckets_displacement = buckets_size * 4 * 2;

	const int buckets_start = (dimension * 4 + byte) * buckets_size;
	uint id = gl_WorkGroupID.x;
	uint pairs_start = id * chunk_size;
	uint pairs_end = min(pairs_start + chunk_size, pairs_count);
		 
	for (uint i = pairs_start; i < pairs_end; ++i) {
		ivec2 pos = particles[pairs[i].first].gridPosition;
		int value = dimension == 0 ? pos.x : pos.y;
		int hash = value >> shift & mask;
		int index = distributed_bucket_indexes[buckets_size * id + hash]++;
		pairs_temp[index] = pairs[i];
	}
}
