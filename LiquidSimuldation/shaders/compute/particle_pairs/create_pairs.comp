#version 430 core
#include "../../common.glsl"

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

layout(std430, binding = 0) buffer ParticlesInput
{
	Particle particles[];
};
layout(std430, binding = 1) buffer GridInput
{
	GridCell grid[];
};
layout(std430, binding = 2) buffer PairsCount
{
	int pairs_count;
};
layout(std430, binding = 3) buffer PairsOutput
{
	PairData pairs[];
};

layout(std140, binding = 0) uniform ConfigInput { Config config; };
layout(location = 1) uniform ivec2 grid_size;
layout(location = 2) uniform float dt;

void main() {
	uint org = gl_GlobalInvocationID.x;
	Range range = GetRange(grid_size, particles[org].grid_position);

	for (int y = range.a.y; y <= range.b.y; ++y) {
		for (int x = range.a.x; x <= range.b.x; ++x) {
			GridCell nghrCell = grid[y * grid_size.x + x];

			for (int i = nghrCell.particles_start; i < nghrCell.particles_end; ++i) {
				if (particles[org].index == particles[i].index) continue;

				vec2 vector = particles[i].position - particles[org].position;
				float dist = length(vector);

				if (dist > config.interactionRange) continue;

				vec2 normal = vector / dist;
				float proximity_coefficient = 1 - dist / config.interactionRange;

				// viscosity
				float inertia = dot(particles[org].velosity - particles[i].velosity, normal);
				if (inertia > 0) {
					vec2 viscosity = 0.5 * proximity_coefficient *
						(config.kLinearViscocity * inertia + config.kQuadraticViscocity * pow(inertia, 2)) *
						normal * dt;
					particles[org].applied_impulse -= viscosity;
				}

				// pressure
				float pressureM = config.k * (particles[org].density - config.restDensity + particles[i].density - config.restDensity);
				float nearPressureM = config.k_near * (particles[org].density_near + particles[i].density_near);
				vec2 pressure = dt * (pressureM * proximity_coefficient + nearPressureM * pow(proximity_coefficient, 2)) * normal;
				particles[org].applied_impulse -= pressure;

				// pair
				if (particles[org].index < particles[i].index) {
					int pair_id = atomicAdd(pairs_count, 1);
					pairs[pair_id].first = int(org);
					pairs[pair_id].second = i;
					pairs[pair_id].normal = normal;
					pairs[pair_id].proximity_coefficient = proximity_coefficient;
				}
			}
		}
	}
}
